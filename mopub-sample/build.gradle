// Copyright 2018-2021 Twitter, Inc.
// Licensed under the MoPub SDK License Agreement
// https://www.mopub.com/legal/sdk-license-agreement/

repositories {
    google()
    mavenCentral()
    flatDir {
        dirs '../libs'
    }
    maven {
        name "Tapjoy's maven repo"
        url "https://tapjoy.bintray.com/maven"
    }
    maven {
        url "https://adcolony.bintray.com/AdColony"
    }
    maven { url 'https://jitpack.io' }  // For Vungle
    maven { url "https://dl.bintray.com/ironsource-mobile/android-sdk" } // For IronSource
    maven { url  "https://chartboostmobile.bintray.com/Chartboost" } // For Chartboost
    jcenter() // Delete it when all third party adapters move to Maven
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

project.group = 'com.mopub'
project.description = '''MoPub Sample App'''
project.version = '5.17.0'

android {
    compileSdkVersion 30
    lintOptions { abortOnError false }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true
    }

    defaultConfig {
        versionCode 89
        versionName version
        minSdkVersion 19
        targetSdkVersion 30
        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android.txt')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "default"
    productFlavors {
        external {
            versionName version
            dimension "default"
        }

    }

    buildTypes {
        all {
            def connectedTestRetryCount = "${System.env.ANDROID_CONNECTED_TEST_RETRY_COUNT}"

            try {
                connectedTestRetryCount = "" + Integer.parseInt(connectedTestRetryCount)
            } catch (NumberFormatException nfe) {
                connectedTestRetryCount = "1"
            }

            buildConfigField "int", "CONNECTED_TEST_RETRY_COUNT", connectedTestRetryCount
        }
        release {
            minifyEnabled true
        }
        debug {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main']
            renderscript.srcDirs = ['src/main']
            res.srcDirs = ['res/main']
            assets.srcDirs = ['assets']
        }

    }

    // Note: You will also need a local.properties file to set the location of the SDK in the same 
    // way that the existing SDK requires, using the sdk.dir property.
    // Alternatively, you can set an environment variable called ANDROID_HOME. There is no 
    // difference between the two methods, you can use the one you prefer.
}

dependencies {
    // Third party Custom Network Adapters
    implementation 'com.adcolony:sdk:4.5.0'
    implementation 'com.google.android.gms:play-services-ads:19.8.0'
    implementation "com.applovin:applovin-sdk:10.1.2"
    implementation "com.chartboost:chartboost-sdk:8.2.0"
    implementation 'com.facebook.android:audience-network-sdk:6.4.0'
    implementation 'com.ironsource.sdk:mediationsdk:7.1.5'
    implementation "com.bytedance.sdk:pangle-sdk:3.4.1.1"
    implementation 'com.tapjoy:tapjoy-android-sdk:12.7.1@aar'
    implementation 'com.unity3d.ads:unity-ads:3.7.1@aar'
    implementation 'com.verizon.ads:android-vas-standard-edition:1.9.0'
    implementation 'com.vungle:publisher-sdk-android:6.9.1'

    // MoPub Custom Network Adapters
    implementation 'com.mopub.mediation:adcolony:4.5.0.0'
    implementation 'com.mopub.mediation:admob:20.0.0.0'
    implementation 'com.mopub.mediation:applovin:10.1.2.0'
    implementation 'com.mopub.mediation:chartboost:8.2.0.3'
    implementation 'com.mopub.mediation:facebookaudiencenetwork:6.4.0.0'
    implementation 'com.mopub.mediation:ironsource:7.1.5.0'
    implementation 'com.mopub.mediation:pangle:3.4.1.1.1'
    implementation 'com.mopub.mediation:tapjoy:12.7.1.1'
    implementation 'com.mopub.mediation:unityads:3.7.1.0'
    implementation 'com.mopub.mediation:verizon:1.9.0.0'
    implementation 'com.mopub.mediation:vungle:6.9.1.0'

    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    implementation 'com.google.android.gms:play-services-base:17.6.0'
    implementation project(':mopub-sdk')

    // QR code reading
    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    // Instrumentation testing-only dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation "androidx.core:core-ktx:1.6.0-alpha01"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}

task wrapper(type: Wrapper) {
    gradleVersion = '5.6.4' //gradle version is required
}
